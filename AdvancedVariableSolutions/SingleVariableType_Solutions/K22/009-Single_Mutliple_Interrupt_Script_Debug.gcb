/*

    Inspect the memory in the simulator to see results.

    Use See https://www.h-schmidt.net/FloatConverter/IEEE754.html

    or    https://babbage.cs.qc.cuny.edu/ieee-754.old/64bit.html

*/
// Chip Settings
#CHIP 18F26K22
    #OPTION Explicit
    

    #DEFINE USART_BAUD_RATE 9600
      #DEFINE USART_TX_BLOCKING
      #DEFINE USART_DELAY OFF


// *****************************************************************************************************

    //! Wait 200 ms to permit any remote serial terminal to settle.
      wait 200 ms
    //! Clear the terminal
      HserSend    0x01 
 
// ---------- Constant and variable definition and main program ---------------------------------

// ************************************************************************

//! Lots of tests!!
//! Maths, timer running, events happening, more maths and show some debug

    //! Uncomment SYSSINGLECALCS_DEBUG to show debug
    // #DEFINE SYSSINGLECALCS_DEBUG

    Dim myCircumference, myRadius, myPi as Single 
    Dim myLong As Long
    Dim ISRcounter as Byte
    
    #script
            cPI = 22/7
    #endscript

    // Initialise the timer and ISR as a test
        InitTimer0 Osc, PRE0_64
        //    Start the Timer
        StartTimer 0

        // Every ~1 sec.  
        SetTimer ( 0 , 3036  )


    HserPrintCRLF   


    Do

        HSerPrintStringCRLF "Initial = " + SingleToString ( cPI )
        HSerPrintStringCRLF "Initial = 0x" + SingleToHex ( cPI )
       
        // Single maths are long running, may need to turn interrupt off
        On Interrupt Timer0Overflow Ignore

        myRadius = 11.5

        // Constant * Single
        myCircumference = cPI * myRadius * 2
            

        HSerPrint "Result   72.28570556 = "
            HSerPrintStringCRLF SingleToString ( myCircumference )
        HSerPrint "Result  0x42909248   =  0x"
            HSerPrintStringCRLF SingleToHex ( myCircumference )

        // Single * Single
        myPi = 3.142857142857143
        myCircumference = myPi  * myRadius * 2
        HSerPrint "Result   72.28570556 = "
            HSerPrintStringCRLF SingleToString ( myCircumference )
        HSerPrint "Result  0x42909248   =  0x"
            HSerPrintStringCRLF SingleToHex ( myCircumference )

        // Single * Single - zero check
        myPi = 10.5
        myRadius = 1000
        myCircumference = myPi  * myRadius * 2  
        HSerPrint "Result  21000.0      = " 
            HSerPrintStringCRLF SingleToString ( myCircumference )
        HSerPrint "Result 0x46A41000    = 0x"
             HSerPrintStringCRLF SingleToHex ( myCircumference )


        // Assign Single to Long 
        myLong = myCircumference 
        HSerPrintStringCRLF "MyLong  = " + LongToString ( myLong )
        HSerPrintCRLF

        On Interrupt Timer0Overflow Call ISR
            
        Wait 5 s

    Loop


    Sub ISR
        ISRcounter++
        If ISRcounter = 0 then 
            HSerPrintStringCRLF "In ISR"
            Wait 10 ms
        End If
    End Sub