'  FILE:    PIC_GCB_16F877a_LCD_NO_LAT_Example.gcb
'  DATE:    07/08/14
'  VERSION: 0.1
'  AUTHOR:  Anobium
'
'This program was written using the GCBASIC IDE for use with the PIC
'16F8777A development board (HCDVBD0003) and our 1602 parallel LCD module
'(HCMODU0013 or HCMODU0038) to display some example text.
'
'You may copy, alter and reuse this code in any way you like, but please leave
'reference to HobbyComponents.com in your comments if you redistribute this code.
'This software may not be used directly for the purpose of selling products that
'directly compete with Hobby Components Ltd's own range of products.
'
'THIS SOFTWARE IS PROVIDED "AS IS". HOBBY COMPONENTS MAKES NO WARRANTIES,
'WHETHER EXPRESS, IMPLIED OR STATUTORY, INCLUDING, BUT NOT LIMITED TO, IMPLIED
'WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE, ACCURACY OR
'LACK OF NEGLIGENCE. HOBBY COMPONENTS SHALL NOT, IN ANY CIRCUMSTANCES, BE LIABLE
'FOR ANY DAMAGES INCLUDING, BUT NOT LIMITED TO, SPECIAL, INCIDENTAL OR
'CONSEQUENTIAL DAMAGES FOR ANY REASON WHATSOEVER.




'Chip Settings. Assumes the development board with with a 16F877a at 4 or 20 Mhz
#CHIP 16F877a,20

'Use LCD in 4 pin mode and define LCD pins
#DEFINE LCD_IO 4
#DEFINE LCD_RW PORTE.1
#DEFINE LCD_RS PORTE.0
#DEFINE LCD_Enable PORTE.2
#DEFINE LCD_DB4 PORTD.4
#DEFINE LCD_DB5 PORTD.5
#DEFINE LCD_DB6 PORTD.6
#DEFINE LCD_DB7 PORTD.7
#DEFINE LCD_SPEED FAST


'Main program

'Clear the LCD
CLS
'Display some text on both lines
Locate 0,0
Print "HOBBY COMPONENTS"
Wait 1 s
CLS
Print ChipNameStr +" @ "+Str(ChipMHz) + "Mhz"
Locate 1,0
Print "by Anobium"





'#define LCDNormalWriteByte myLCDNormalWriteByte
Sub myLCDNormalWriteByte(In LCDByte)

' *****    This sub modified by William Roth *****

'Reduced enable pulse to 1 us
'Added conditional delay for for LCD_SLOW

    #IFNDEF LCD_NO_RW
        #IFDEF LCD_IO 4,8
            Wait Until LCDReady
            'Write mode
            Set LCD_RW Off
        #ENDIF
    #ENDIF

    #IFDEF LCD_IO 2
        'Swap byte; Most significant NIBBLE sent first
        LCD2_NIBBLEOUT SWAP4(LCDByte)
        'Less Significant NIBBLE output
        LCD2_NIBBLEOUT LCDByte
    #ENDIF

    #IFDEF LCD_IO 4
        'Set pins to output
        #IFNDEF LCD_LAT
            Dir LCD_DB4 Out
            Dir LCD_DB5 Out
            Dir LCD_DB6 Out
            Dir LCD_DB7 Out
        #ENDIF
        #IFDEF LCD_LAT
            Dir _LCD_DB4 Out
            Dir _LCD_DB5 Out
            Dir _LCD_DB6 Out
            Dir _LCD_DB7 Out
        #ENDIF


        'Write upper nibble to output pins
        Set LCD_DB4 Off
        Set LCD_DB5 Off
        Set LCD_DB6 Off
        Set LCD_DB7 Off
        If LCDByte.7 On Then Set LCD_DB7 On
        If LCDByte.6 On Then Set LCD_DB6 On
        If LCDByte.5 On Then Set LCD_DB5 On
        If LCDByte.4 On Then Set LCD_DB4 On

        '************************************
        #IFDEF LCD_SLOW
            PulseOut LCD_enable, 2 us
            Wait 1 us
        #ENDIF

        #IFNDEF LCD_SLOW
            PulseOut LCD_enable, 1 us
            Wait 1 us
        #ENDIF
        '*************************************

        'Write lower nibble to output pins
        Set LCD_DB4 Off
        Set LCD_DB5 Off
        Set LCD_DB6 Off
        Set LCD_DB7 Off
        If LCDByte.3 On Then Set LCD_DB7 On
        If LCDByte.2 On Then Set LCD_DB6 On
        If LCDByte.1 On Then Set LCD_DB5 On
        If LCDByte.0 On Then Set LCD_DB4 On


        'Pulse enable pin
        '*********************************
        #IFDEF LCD_SLOW
            PulseOut LCD_enable, 2 us
            Wait 1 us
        #ENDIF

        #IFNDEF LCD_SLOW
            PulseOut LCD_enable, 1 us
            Wait 1 us
        #ENDIF
        '***********************************

        'Set data pins low again
        Set LCD_DB7 Off
        Set LCD_DB6 Off
        Set LCD_DB5 Off
        Set LCD_DB4 Off
        '*******************************************
        'Character rate = 10K
        #IFDEF LCD_SLOW
            'character delay time
            Wait 65 us
        #ENDIF

        'Charcter rate = 20K
        #IFNDEF LCD_SLOW
            'character delay time
            Wait 17 us
        #ENDIF
        '*********************************************

    #ENDIF

    #IFDEF LCD_IO 8
        'Set data port to output, and write a value to it
        #IFNDEF LCD_LAT
            Dir LCD_DATA_PORT Out
        #ENDIF
        #IFDEF LCD_LAT
            Dir _LCD_DATA_PORT Out
        #ENDIF
        LCD_DATA_PORT = LCDByte

        '********Added / Changed *************************
        #IFDEF LCD_SLOW
            Wait 2 us
            PulseOut LCD_enable, 2 us
            Wait 2  us
            LCD_DATA_PORT = 0
            'character rate = 10K
            Wait 80 us
        #ENDIF

        'Fast
        #IFNDEF LCD_SLOW
            Wait 1 us
            PulseOut LCD_enable, 1 us
            Wait 1  us
            LCD_DATA_PORT = 0
            'character rate = 20K
            Wait 32 us
        #ENDIF
        '*********************************

    #ENDIF

End Sub
