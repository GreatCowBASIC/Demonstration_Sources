'''A demonstration program for GCBASIC
'''---------------------------------------------------------------------------------
''' This program shows how to set the PWM in fixed frequency and duty AND remap the PWM to the LED
'''
''' Change TCCR0B to change the frequency.  OCR0BL controls the DUTY
'''
'''@author  Evan Venn
'''@licence GPL
'''@version 1.0
'''@date    03/07/2020
'''********************************************************************************

/*
Diagram Author : Ardunaut
                 __________
           VCC--|1       14|--GND (TPI CLK)
           PA0--|2       13|--PB3 (CDC TX) -->USART_Transmit()
(TPI DATA) PA1--|3       12|--PB2 (CDC RX) <--USART_Receive()
   (RESET) PA2--|4       11|--PB1 (BUTTON)
           PA3--|5       10|--PB0
           PA4--|6        9|--PA7
     (LED) PA5--|7        8|--PA6
                \__________/
        Atmel ATtiny104 Xplained Nano
*/

; ----- Configuration
#chip  TINY104, 4
#option Explicit


  #Define SWITCH  portb.1
  #Define LED     porta.5

  'Set Internal Pullup for SWITCH port
  PUEB.PORTB1 = 1
  'Set direction of Switch
  Dir SWITCH in

  'Use Volatile to ensure the compiler does not try to optimize the code
  #option Volatile LED
  'Set direction of LED
  Dir LED out


  COM0B1 = 1: COM0B0 = 0                       ' Set OC0B1 Low for a port that is PULLED HIGH, examine COM0B1 and COM0B0 for other options
  WGM00  = 1: WGM01 = 1                        ' Set OC0B1 Low , Fast 10bit PWM mode
  WGM02  = 1: TCCR0B = TCCR0B or 2             ' Fast PWM with OCR0B as TOP;  clkI/O/8 (From prescaler)

  'remap the COM0B1/PWM to the LED
  REMAP = 1

  'Main loop
  'Simply index OCR0BL to change the DUTY %

  Dim OCR0B as word
  OCR0B = 0

  do
        '10 bit addressing by using 1024
        repeat 1024
          OCR0BH = OCR0B_h
          OCR0BL = OCR0B
          OCR0B++
          wait 1 ms
        end repeat

        repeat 1024
          OCR0BH = OCR0B_h
          OCR0BL = OCR0B
          OCR0B--
          wait 1 ms
        end repeat

  Loop
