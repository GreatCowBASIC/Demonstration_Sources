 '''A demonstration program for GCBASIC.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program searches for I2C devices and displays their adresses 
'''to the attached PC terminal.
'''This program uses the software implementation of I2C and TWI for the Microchip 
'''and ATMEL microcontrollers.  
'''The microcontrollers must have a suitable I2C or TWI module.
'''The hardware serial connections are shown in the program.

'''@author  EvanV  HiB
'''@licence GPL
'''@version 1.3
'''@date   12/3/2025
'''********************************************************************************
 
 #chip 18f4431,4
  #option explicit


  ' Define I2C settings - CHANGE PORTS
         #define I2C_MODE master  
         #define I2C_DATA PORTc.4
         #define I2C_CLOCK PORTc.3
         #define I2C_DISABLE_INTERRUPTS ON

 ' THIS CONFIG OF THE SERIAL PORT WORKS WITH max232 THEN TO PC
 ' USART settings
         #define USART_BAUD_RATE 9600
         Dir PORTc.6 Out
         #define USART_DELAY 1 ms
         #define USART_TX_BLOCKING
         wait 500 ms

;----- Variables
  Dim DISPLAYNEWLINE as Byte


; ----- Main body of program commences here.
         ' Now assumes Serial Terminal is operational
  dim DeviceID as byte

  HSerPrintCRLF
  HSerPrint "Software I2C "
  HSerPrintCRLF 2

  HSerPrint "     "
  for DeviceID = 0 to 15
    HSerPrint hex(deviceID)
    HSerPrint " "
  next
  for DeviceID = 0 to 255
    DisplayNewLine = DeviceID % 16
    if DisplayNewLine = 0 Then
      HSerPrintCRLF
      HserPrint hex(DeviceID)
      HSerPrint ": "
    end if
    HSerPrint " "
    I2CStart
    I2CSend ( deviceID )

    if I2CSendState = True  then
      HSerPrint   hex(deviceID)
    else
      HSerPrint "--"
    end if
    I2CSend ( 0 )
    i2cstop


  Next
  HSerPrintCRLF 2
  HSerPrint   "End of Search"
  HSerPrintCRLF 2
  END