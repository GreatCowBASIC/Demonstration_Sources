'''A demonstration program for GCBASIC.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program reads the value of a DS18B20 connected to Portc.7.  The DS18B20 must be inserted correctly.
'''The LCD connection is shown in the program below - the standard connection for the GCB Demo Board
''':
'''@author 	EvanV plus works of others
'''@licence	GPL
'''@version	1.1a
'''@date   	26.12.2015
'''********************************************************************************

' ----- Configuration
#CHIP 16f1937,16
#OPTION Explicit
#INCLUDE <ds18b20.h>

'Setup LCD Parameters
#DEFINE LCD_IO 4

#DEFINE LCD_SPEED SLOW

' ----- Define Hardware settings
#DEFINE LCD_RS PORTE.0
#DEFINE LCD_RW PORTE.1
#DEFINE LCD_Enable PORTE.2
#DEFINE LCD_DB4 PORTd.4
#DEFINE LCD_DB5 PORTd.5
#DEFINE LCD_DB6 PORTd.6
#DEFINE LCD_DB7 PORTd.7

' ----- Constants
' DS18B20 port settings
#DEFINE DQ Portc.7

' ----- Variables
' a variabler to handle the temperature calculations
Dim TempC_100 As Word
Dim CCOUNT, SIGNBIT, WHOLE, FRACT, DIG As Byte


// ----- Main body of program commences here.

ccount = 0
CLS
Print "GCBasic 2019"
Locate 1,0
Print "DS18B20 Demo"
Wait 2 s


Do Forever
    ' The function readtemp returns the integer value of the sensor
    DSdata = ReadTemp

    ' Display the integer value of the sensor on the LCD
    Locate 0,0
    Print Hex(ccount)
    Print " Ceil"
    Locate 0,8
    Print DSdata
    Print CHR(223)+"C"



    ' Display the integer and decimal value of the sensor on the LCD

    ' The function readtemp12 returns the raw value of the sensor.
    ' The sensor is read as a 12 bit value therefore each unit equates to 0.0625 of a degree
    DSdata = ReadTemp12
    SignBit = DSdata / 256 / 128
    If SignBit = 0 Then Goto Positive
    ' its negative!
    ' take twos comp
    DSdata = ( DSdata # 0xffff ) + 1

Positive:

    ' Convert value * 0.0625. Mulitple value by 6 then add result to multiplication of the value with 25 then divide result by 100.
    TempC_100 =  DSdata * 6
    DSdata = ( DSdata * 25 ) / 100
    TempC_100 = TempC_100 + DSdata

    Whole = TempC_100 / 100
    Fract = TempC_100 % 100
    If SignBit = 0 Then Goto DisplayTemp
    Print "-"

DisplayTemp:
    Locate 1,0
    Print Hex(ccount)
    Print " Real"
    Locate 1,8
    Print Str(Whole)
    Print "."
    ' To ensure the decimal part is two digits
    Dig = Fract / 10
    Print Dig
    Dig = Fract % 10
    Print Dig
    Print CHR(223)
    Print "C"
    Wait 2 s
    ccount++

Loop
