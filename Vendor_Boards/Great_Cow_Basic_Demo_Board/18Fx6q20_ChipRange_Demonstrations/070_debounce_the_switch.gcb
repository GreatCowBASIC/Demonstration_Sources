/*
    This demonstration uses a simple software routine to avoid the initial noise on the switch
    pin. The code will inspect the switch status, but should overcome most of the noise from a switch.

    Some switches are worse than others.

    When the switch is held down and released, LEDs will toggle.


    ************************************************************************
    @author  EvanV
    @licence GPL
    @version 1.00
    @date    04-02-2024
*/
// ----- Configuration
//Chip Settings.
#CHIP 18F16Q20, 4
#OPTION Explicit

/*
    Hardware
    -------------------PORTA----------------
    Bit#:  -7---6---5---4---3---2---1---0---
    IO:---------------------SW---------ADC-- 
    -----------------------------------------


    -------------------PORTB----------------
    Bit#:  -7---6---5---4---3---2---1---0---
    IO:---------------------------------
    -----------------------------------------

    ------------------PORTC-----------------
    Bit#:  -7---6---5---4---3---2---1---0---
    IO:LED-LED-LED-LED------------------
    ----------------------------------------
  */

// Define constants to make things easier. We can reuse these at any time.
    #DEFINE LEDPORT LATC
    #DEFINE LEDD7 PORTC.7
    #DEFINE LEDD6 PORTC.6
    #DEFINE LEDD5 PORTC.5
    #DEFINE LEDD4 PORTC.4

// Set the ports as an output
    Dir LEDD7 Out
    Dir LEDD6 Out
    Dir LEDD5 Out
    Dir LEDD4 Out

    #DEFINE POTENTIOMETER PORTA.0
    // This is the PORTA.0 port
    Dir POTENTIOMETER In

    #DEFINE SWITCHIN      PORTA.3
    Dir     SWITCHIN      In
    #DEFINE DOWN          1

// *****************************************************************************************************
    // Main program commences here.. everything before this is setup for the board.
    // Debouncing a switch

    Do Forever
        Wait 10 ms

        'keyPressed is a Function that returns the state
        If  funcKeyPressed() = TRUE Then

            'push button pressed
            LEDD7 = !LEDD7
            LEDD6 = !LEDD6
            LEDD5 = !LEDD5
            LEDD4 = !LEDD4

        End If

    Loop


    // This is function, it returns the state of the switch
    Function  funcKeyPressed as Bit
        
        // Define some variables
        Dim CurrentSwitchState As Bit
        Dim LastSwitchState As Bit

        wait 1 ms

        // Is the switch pressed down ?
        If ( SWITCHIN = DOWN ) Then
            
            // Set the state
            CurrentSwitchState = TRUE

        Else

            // So, switch is therefore UP/released
            CurrentSwitchState = FALSE

        End If

        // This check ensures the state of the switch ....
        // ensure the switch has changed state
        If (CurrentSwitchState <> LastSwitchState) Then

            // Set the last state
            LastSwitchState = CurrentSwitchState

            // Return the state value - returns either TRUE or FALSE
            funcKeyPressed =  CurrentSwitchState

        Else
            // Switch is not down
            funcKeyPressed = FALSE

        End If

    End Function
