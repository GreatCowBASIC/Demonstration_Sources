#CHIP 18F16Q20, 16
#OPTION Explicit


    // PIC PPS Tool version: 0.0.6.5
    #define PPSToolPart 18F16Q20

    Sub InitPPS
            
            // Module: CCP1
            RC4PPS = 0x0005    // CCP1 > RC4
            CCP1PPS = 0x0014    // RC4 > CCP1 (bi-directional)
            RC5PPS = 0x0005    // CCP1 > RC5
            CCP1PPS = 0x0015    // RC5 > CCP1 (bi-directional)
            RC6PPS = 0x0005    // CCP1 > RC6
            CCP1PPS = 0x0016    // RC6 > CCP1 (bi-directional)
            RC7PPS = 0x0005    // CCP1 > RC7
            CCP1PPS = 0x0017    // RC7 > CCP1 (bi-directional)

            // Module: UART pin directions
            Dir PORTB.7 Out    ' Make TX1 pin an output
            // Module: UART1
            RB7PPS = 0x0013    // TX1 > RB7

        End Sub
    // Add this code segment to your GCBASIC source program
/*
    Hardware
    -------------------PORTA----------------
    Bit#:  -7---6---5---4---3---2---1---0---
    IO:    -----------------SW---------ADC-- 
    -----------------------------------------

    -------------------PORTB----------------
    Bit#:  -7---6---5---4---3---2---1---0---
    IO:    TX-----------^-------------------
    ------------------VDDIO3---------------- You must apply correct Voltage
    
    ------------------PORTC-----------------
    Bit#:  -7---6---5---4---3---2---1---0---
    IO:    LED-LED-LED-LED------^-----------
    --------------------------VDDIO2-------- You must apply correct Voltage

  */

// Setup USART
    #DEFINE USART_BAUD_RATE 9600
    #DEFINE USART_TX_BLOCKING
    #DEFINE USART_DELAY OFF


// Define constants to make things easier. We can reuse these at any time.
    #DEFINE LEDPORT LATC
    #DEFINE LEDD7 PORTC.7
    #DEFINE LEDD6 PORTC.6
    #DEFINE LEDD5 PORTC.5
    #DEFINE LEDD4 PORTC.4

// Set the ports as an output
    Dir LEDD7 Out
    Dir LEDD6 Out
    Dir LEDD5 Out
    Dir LEDD4 Out

    #DEFINE POTENTIOMETER PORTA.0
    // This is the PORTA.0 port
    Dir POTENTIOMETER In

    #DEFINE SWITCHIN      PORTA.3
    Dir     SWITCHIN      In
    #DEFINE DOWN          0


' ------ Start of main Program

    #DEFINE DEVELOPER_PWM_DEBUG     
    #DEFINE PWM_FREQ    38
    #DEFINE PWM_DUTY    50

    #DEFINE CALCULATED_PWM_DUTY  ( ( 255 * PWM_DUTY ) / 100  )

    // These variables are in pmw.h used to calc the registers 
    PWMDuty = 0
    PWMResolution = 0

                HSerSend 254  // clear terminal
                wait 100 ms
                HSerSend 254  // clear terminal
                wait 100 ms
                HSerPrintStringCRLF "Hardware CCP/PWM Discovery"
                

                HSerPrintStringCRLF "Before PWMon = Fixed Mode PWM"
                ShowDetails
                
    PWMOn       
                HSerPrintStringCRLF "After PWMon"
                ShowDetails
                HserPrintCRLF 2
                HSerPrintStringCRLF "If no Fixed Mode PWM signal... are the PWM ports set to OUT? Correct PPS?"
                HserPrintCRLF 2

                Repeat 10
                    wait 500 ms
                    HserSend "."
                End Repeat

                HserPrintCRLF
                HSerPrintStringCRLF "PWMOff"
    PWMoff

                HserPrintCRLF
                HSerPrintStringCRLF "After PWMoff"
                ShowDetails
                HSerPrintStringCRLF "Fixed mode PWM signal will now stop"
                HserPrintCRLF
                wait 2 s

                HSerPrint "Issue `HPWM 1, PWM_FREQ,"
                HSerprint CALCULATED_PWM_DUTY
                HSerPrintStringCRLF "` command.  The third parameter range is 0 to 255, where 127 = 50%"
                HserPrintCRLF

    HPWM 1, PWM_FREQ, CALCULATED_PWM_DUTY           
    // HPWM_CCPTimerN 1, PWM_FREQ, CALCULATED_PWM_DUTY, 2  // Equates to using Timer2

    //! uncomment to add this to the pwm.h section as a test, if no PWM signal    
    // #define AddHPWMCCPExit1 CCP1CON  = CCPCONCache
    
                HSerPrintStringCRLF pad("", 80, "*")
                HSerPrint "After `HPWM 1, PWM_FREQ,"
                HSerprint CALCULATED_PWM_DUTY
                HSerPrintStringCRLF "` command"

                HSerPrintCRLF 2
                HSerPrintStringCRLF " As HPWM() used same Frq & Duty as `fixed mode PWM`, then, CCP1CON should be equal to CCPCONCache"
                HSerPrintCRLF 
                HSerPrintStringCRLF "No signal ?"
                HSerPrintStringCRLF "  1. Has ChipFamilyPWMCCP1CON16Handler, in PWM.h, been updated for this chip family ?"
                HSerPrintStringCRLF "     If the specific chip has ONLY ONE CCP then the section ChipFamilyPWMCCP1CON16Handler must be updated"
                HSerPrintStringCRLF "     So, check the datasheet for CCP1 only.. so, search for CCP2 if not found then only one CCP"
                HSerPrintCRLF
                HSerPrintStringCRLF "  2. Is CCPTMRS0 correct for timer2? if not, is PWMTimerVariant in DAT file the correct value ?"
                HSerPrintCRLF
                HSerPrintStringCRLF "  3. Examine SETUPCCPPWMREGISTERS section in pwm.h... there is clearly a library issue"
                HserPrintCRLF
                HSerPrintStringCRLF "If PWM is working.. Then, all is good!"


                HSerPrintStringCRLF pad("", 80, "*")
                ShowDetails

                HserPrintCRLF
                HSerPrintStringCRLF "Completed - examine results"

                Do
                Loop












Sub ShowDetails

    #IFDEF VAR(PPSLOCK)
        //! If this causes and error .. remove the first one. Let the be the only #startup instruction
        #startup InitPPS, 85
    #endif

    HSerPrint "Constants"               : HserPrintCRLF
        HserPrint "PWM_FREQ:        "
            HserPrint PWM_FREQ          : HserPrintCRLF
        HserPrint "PWM_DUTY:        "
            HserPrint PWM_DUTY          : HserPrintCRLF
        HserPrint "USE_HPWMCCP1:    "
            HserPrint USE_HPWMCCP1      : HserPrintCRLF
        HserPrint "USE_HPWM_TIMER2: "
            HserPrint USE_HPWM_TIMER2   : HserPrintCRLF
        #If def(DisableCCPFixedModePWM) 
            HserPrint "DisableCCPFixedModePWM: "
                HserPrint DisableCCPFixedModePWM    : HserPrintCRLF
        #endif

    HserPrintCRLF
    HserPrint "Registers": HserPrintCRLF
        PReg("CCP1CON      ", CCP1CON, "CCP Control Register" )
        PReg("CCPCONCache  ", CCPCONCache, "Cached register used to by PWMon" )
        
        PReg("PR2          ", PR2, "Timer Period" )
        Preg("T2CON        ", T2CON ,"Timer2 control")
        Preg("T2CLKCON     ", T2CLKCON, "Clock Source Selection" )
        Preg("CCPR1H       ", CCPR1H, "Duty cycle H" )
        Preg("CCPR1L       ", CCPR1L, "Duty cycle L")
        Preg("CCPTMRS0     ", CCPTMRS0, "Timer Selection")
        
        
    HserPrintCRLF
    HserPrint "Variables": HserPrintCRLF
        dim _PWMTimerSelected as Byte
        Preg( "_PWMTimerSelected", _PWMTimerSelected, "Selected Timer")
        Preg( "PWMDuty          ", PWMDuty, "[WORD] used in calculateDuty() in pwm.h ")
        Preg( "PWMResolution    ", PWMResolution, "[WORD] used in calculateDuty() in pwm.h")
        
        

End Sub

Macro PReg ( xstr, xreg , comment )


        HSerPrint "  "+xstr+": "
            HSerPrint ByteToBin(xreg): HSerPrint ", 0x"+Hex(xreg):HSerPrint ", "+ Pad(Str(xreg),10," "  ):HserPrint chr(9)+comment: HserPrintCRLF


End Macro



/* GCBASIC Optimisation file - this will optimise PWM operations by disabling the PWM operations on the PWM channel and Timer that is set to FALSE
                             to enable PWM and Timer operations set to FALSE

           Optimise CCP/PWM    
*/

           #DEFINE USE_HPWMCCP1 TRUE
           #DEFINE USE_HPWMCCP2 FALSE
           #DEFINE USE_HPWMCCP4 FALSE
           #DEFINE USE_HPWMCCP5 FALSE

//         Optimise PWM
           #DEFINE USE_HPWM1 TRUE
           #DEFINE USE_HPWM2 FALSE
           #DEFINE USE_HPWM3 FALSE
           #DEFINE USE_HPWM4 FALSE
           #DEFINE USE_HPWM5 FALSE
           #DEFINE USE_HPWM6 FALSE
           #DEFINE USE_HPWM7 FALSE
           #DEFINE USE_HPWM8 FALSE

//         Optimise Timers associated with PWM operations
           #DEFINE USE_HPWM_TIMER2 TRUE
           #DEFINE USE_HPWM_TIMER4 FALSE
           #DEFINE USE_HPWM_TIMER6 FALSE
