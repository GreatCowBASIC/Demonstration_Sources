'''A demonstration program for GCBASIC.
'''--------------------------------------------------------------------------------
'''This program recreation of Microchip Curiosity firmware for the 16F1619
'''
'''
''' Based on cvd_touch_button_16f1619 by Trevor B Roydhouse
''' 
''' Microchip Curiosity 8/14/20 pin 8 bit demo board
'''  o PIC16F1619 (http://microchip.com/pic16f1619)
'''  o LEDs D4-D7 on RA5, RA1/PGEC, RA2, RC5
'''  o Switch SWITCH1 on RC4
'''  o Switch S2 on RA3/MCLRE
'''  o Touch Switch S3 on RC1/AN5
'''  o Potentiometer POT1 on RC0/AN4
'''
''' An Implementation of Microchip Application Note AN1298
''' o  Capacitive Touch Using Only an ADC using the concept of Capacitive Voltage Divider ("CVD")
''' o  http://ww1.microchip.com/downloads/en/AppNotes/01298A.pdf
'''
''' On startup LEDs D4-D7 turn on and off
''' LED D6 turns on while S3 touch sensor is touched
''' LED D6 turns off when S3 touch sensor is not touched
''' 
''' 
'''@author    Evan R. Venn   
'''@license   GPL
'''@version   1.00
'''@date      2023-04-02
'''********************************************************************************

' ---- Configuration

'Microcontroller @ 32MHz
#CHIP 16F1619, 32

'MCLRE set on so that switch S2 can reset microcontroller
#CONFIG MCLRE_ON


'Ensure all variables are explicitly defined
#OPTION Explicit

' ---- Define hardware settings

#DEFINE SWITCH1 PORTC.4
Dir SWITCH1 In

#DEFINE LED4 PORTA.5
Dir PORTA.5 Out

#DEFINE LED5 PORTA.1
Dir PORTA.1 Out

#DEFINE LED6 PORTA.2
Dir PORTA.2 Out

#DEFINE LED7 PORTC.5
Dir PORTC.5 Out

#DEFINE POT1 AN4
Dir PORTC.0 In

'startup wait in seconds
#DEFINE WAITSECS 1

' ---- Variables

Dim ADCvalueWord As Word

' ---- Main body of program

'LED init on program startup
Wait WAITSECS s
Set LED4 = 1
Set LED5 = 1
Set LED6 = 1
Set LED7 = 1
Wait WAITSECS s
Set LED7 = 0
Set LED6 = 0
Set LED5 = 0
Set LED4 = 0

Set ADCvalueWord = 0

'Program infinite loop
Do Forever

    ' Read mTouch state
    ReadmTouch ( LED6 )
    
    ' Set LED to state of switch
    LED4 = !SWITCH1

Loop

Macro ReadmTouch ( LedOut )

    ' ---- Constants

    ' AN5/RC1 - sensor channel
    #DEFINE SENSORCHANNEL PORTC.1	
    Dir SENSORCHANNEL In

    ' AN6/RC2 - secondary (dummy) channel
    #DEFINE SECONDARYCHANNEL PORTC.2
    Dir SECONDARYCHANNEL Out

    IntOff

    Repeat 5

            'AN1298 - Step 1: Set secondary (dummy) channel high (VDD)
            'set as output
            Dir SECONDARYCHANNEL Out	
            'set as open drain
            Set ODCONC.2 = 1
            'set as digital
            Set ANSELC.2 = 0
            'set high (VDD)
            Set SECONDARYCHANNEL = 1

            'AN1298 - Step 2: Select secondary channel on ADC (charge CHOLD to VDD)
            'ADC channel select -> AN6/RC2 (bits 2-6)
            Set ADCON0 = 0b00011001
            Set ADCON1 = 0b11010000

            'AN1298 - Step 3: Set sensor pin as output, low (GND) to eliminate any charge
            'Set as output
            Dir SENSORCHANNEL Out
            'Set as digital
            Set ANSELC.1 = 0
            'set low (ground)
            Set SENSORCHANNEL = 0

            'AN1298 - Step 4: Set sensor pin as input
            'Set as input
            Dir SENSORCHANNEL In	
            'Set as analog
            Set ANSELC.1 = 1

            'AN1298 - Step 5: Select sensor channel on ADC (V-div sensor:CHOLD)
            'ADC channel select -> AN5/RC1 (bits 2-6)
            Set ADCON0 = 0b00010100
            
            'Enable ADC (bit 0)
            ADCON0.0 = 1

            'Delay to allow ADC voltage to stabilise
            // Wait AD_Delay		

            'AN1298 - Step 6: Start ADC conversion
            'Start ADC conversion (set go/done bit 1 = 1)
            Set ADCON0.1 = 1  	
            'Wait for ADC conversion (done bit 1 = 0)
            Wait While ADCON0.1 = 1

            'AN1298 - Step 7: Read ADC result
            ADCvalueWord = ADRESL
            ADCvalueWord_H = ADRESH
            ADCvalueWord = Average ( ADCvalueWord, ADCvalueWord )
            wait 1 ms

    End Repeat
    'Action result
    If ADCvalueWord > 5 Then
        'If touching S3 touch button
        LedOut = 1
    Else
        LedOut = 0
    End If

    IntOn

End Macro