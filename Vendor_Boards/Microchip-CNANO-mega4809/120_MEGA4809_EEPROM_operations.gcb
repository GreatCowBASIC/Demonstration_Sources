/*

This program write 256 EEPROM bytes - select from Random number, a fixed number, or a range ( 255 to 0 )

The program writes and reads the EEPROM.

The program will output the results on a serial terminal as follows - this is a range



    EEPROM Test and Show Results 

          00 01 02 03 04 05 06 07 08 09 0A 0B 0C 0D 0E 0F 
    000:  FF FE FD FC FB FA F9 F8 F7 F6 F5 F4 F3 F2 F1 F0
    010:  EF EE ED EC EB EA E9 E8 E7 E6 E5 E4 E3 E2 E1 E0
    020:  DF DE DD DC DB DA D9 D8 D7 D6 D5 D4 D3 D2 D1 D0
    030:  CF CE CD CC CB CA C9 C8 C7 C6 C5 C4 C3 C2 C1 C0
    040:  BF BE BD BC BB BA B9 B8 B7 B6 B5 B4 B3 B2 B1 B0
    050:  AF AE AD AC AB AA A9 A8 A7 A6 A5 A4 A3 A2 A1 A0
    060:  9F 9E 9D 9C 9B 9A 99 98 97 96 95 94 93 92 91 90
    070:  8F 8E 8D 8C 8B 8A 89 88 87 86 85 84 83 82 81 80
    080:  7F 7E 7D 7C 7B 7A 79 78 77 76 75 74 73 72 71 70
    090:  6F 6E 6D 6C 6B 6A 69 68 67 66 65 64 63 62 61 60
    0A0:  5F 5E 5D 5C 5B 5A 59 58 57 56 55 54 53 52 51 50
    0B0:  4F 4E 4D 4C 4B 4A 49 48 47 46 45 44 43 42 41 40
    0C0:  3F 3E 3D 3C 3B 3A 39 38 37 36 35 34 33 32 31 30
    0D0:  2F 2E 2D 2C 2B 2A 29 28 27 26 25 24 23 22 21 20
    0E0:  1F 1E 1D 1C 1B 1A 19 18 17 16 15 14 13 12 11 10
    0F0:  0F 0E 0D 0C 0B 0A 09 08 07 06 05 04 03 02 01 00

    End of EEProm Program and Display



*/
#chip mega4809            //  Declare the Target Processor and Speed
#option explicit          //  Require Explicit declaration of Variables
  

#DEFINE USART3_BAUD_RATE 9600
#DEFINE USART3_TX_BLOCKING
#DEFINE USART3_DELAY OFF


'*****************************************************************************************************
'Main program commences here.. everything before this is setup for the board.
'Now assumes Serial Terminal is operational

Dim DisplayNewLine, Outdata As Byte
Dim EEPromAddress, Indata as Word

Wait 1 s

Do Forever

    HSerPrintCRLF
    HSerPrint "EEPROM Test and Show Results "
    HSerPrintCRLF 2

    HSerPrint "      "

    For EEPromAddress = 0 To 15
        HSerPrint Hex(EEPromAddress)
        HSerPrint " "
    Next

    For EEPromAddress = 0 To ChipEEProm -1
        DisplayNewLine = EEPromAddress % 16
        If DisplayNewLine = 0 Then
            HSerPrintCRLF
            HSerPrint right(Hex(EEPromAddress_H),1)
            HSerPrint Hex(EEPromAddress)
            HSerPrint ": "
        End If
        HSerPrint " "

        
        Outdata = Random 
        // Outdata = 0x55    // If you DON'T believe put your own value here!
        // Outdata = 255 - EEPromAddress    // If you DON'T believe put your own value here!

        // Writes
        EPWrite ( EEPromAddress, Outdata )
        // Reads
        InData = 0
        EPRead ( EEPromAddress, Indata )
    
        If Outdata = Indata Then
            HSerPrint  Hex(Indata)
        Else
            HSerPrint  "--"
        End If

    Next


    HSerPrintCRLF 2
    HSerPrint   "End of EEProm Program and Display"
    HSerPrintCRLF 2

    Wait 5 s
Loop
