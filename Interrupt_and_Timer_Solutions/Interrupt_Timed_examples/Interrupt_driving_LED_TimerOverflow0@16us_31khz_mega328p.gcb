'''A demonstration program for GCBASIC.
'''--------------------------------------------------------------------------------------------------------------------------------
'''     Description.
'''    A demonstration program for GCBASIC.
'''    This adjusts the power to the LED over 2.5 seconds, then to zero power again over 2.5 seconds with a hold of 1 second between each ramp up/dowwn.
'''    Timer0 overflows every 15.8us. We use this overflow to create the PWM'ed LED.
'''    The LED is connected to Portb.5 via appropiate resistors.
''':
'''     This file is part of the GCBASIC compiler.
'''
'''     This demonstration code is distributed in the hope that it will be useful,
'''     but WITHOUT ANY WARRANTY'''  without even the implied warranty of
'''     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
'''     GNU General Public License for more details.
'''
'''
'''@author  EvanV
'''@licence GPL
'''@version 1.0b
'''@date    19.04.2017
'''********************************************************************************

#chip mega328p, 16
#option explicit


#define LED PORTB.5
dir LED out

'Call the initialisation routine
InitLEDControl

;----- Variables
  Dim LEDSPEED, PWMCOUNTER, PWMCOUNTER as Byte


'Main routine
  Do
      ;Increase LED to full over 2.5 seconds
      For LEDSpeed = 0 to 100
          Wait 25 ms
      Next
      ;Hold LEDPower
      Wait 1 s
      ;Decrease LED to zero over 2.5 seconds
      For LEDSpeed = 100 to 0 step - 1
          Wait 25 ms
      Next
      ;Hold LEDPower
      Wait 1 s
  Loop
  end



'Setup routine
Sub InitLEDControl
    set LED off
    'Clear variables
    LEDSpeed = 0
    PWMCounter = 0

'   Add the handler for the interrupt
    On Interrupt Timer0Overflow Call InterruptHandler

    'Set up the timer
    InitTimer0 Osc, PS_1
    StartTimer 0

End Sub


'PWM sub
'This will be called when the Timer overflows
Sub InterruptHandler

    If LEDSpeed > PWMCounter Then
        Set LED On
    Else
        Set LED Off
    End If
    PWMCounter += 1
    If PWMCounter = 100 Then PWMCounter = 0

End Sub
