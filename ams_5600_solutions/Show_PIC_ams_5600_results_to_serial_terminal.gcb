'''
'''******************************************************************
'''
'''
'''
'''  PIC: 16F18855
'''  Compiler: GCB
'''  IDE: GCB@SYN
'''
'''  Date: 13.3.2016
'''


' ----- Configuration
'Chip Settings.
#CHIP 16f18855,32
#OPTION Explicit

'Generated by PIC PPS Tool for GCBASIC
'PPS Tool version: 0.0.5.2
'PinManager data: 09/02/2017
'
'Template comment at the start of the config file
'
#STARTUP InitPPS, 85

Sub InitPPS
    UNLOCKPPS

    'Module: EUSART
    'TX > RC0
    RC0PPS = 0x0010
    'RC0 > TX (bi-directional)
    TXPPS = 0x0008
    'Module: MSSP1
    'RC3 > SDA1
    SSP1DATPPS = 0x0013
    'SDA1 > RC3 (bi-directional)
    RC3PPS = 0x0015
    'SCL1 > RC4
    RC4PPS = 0x0014
    'RC4 > SCL1 (bi-directional)
    SSP1CLKPPS = 0x0014

    LOCKPPS
End Sub
'Template comment at the end of the config file



#DEFINE SWITCH_DOWN         0
#DEFINE SWITCH_UP           1

#DEFINE SWITCH      PORTA.5

#DEFINE USART_BAUD_RATE 19200
#DEFINE USART_TX_BLOCKING


' ----- Define Hardware settings for hwi2c
' Define I2C settings - CHANGE PORTS if required for your specific device.
#DEFINE HI2C_BAUD_RATE 40
#DEFINE HI2C_DATA PORTC.3
#DEFINE HI2C_CLOCK PORTC.4
'Initialise I2C Master
'I2C pins need to be input for I2C module
Dir HI2C_DATA In
Dir HI2C_CLOCK In

HI2CMode Master

#include <AMS_5600.h>

'Power the Device - YOU NEED TO POWER CYCLE THE DEVICE EVERY TIME!!
DIR PORTC.7 Out
PORTC.7 = 1
Wait 100 ms

DIM valor, oldvalor, regvalues as Word

HSerPrintCRLF
HSerPrint "Started device address: 0x"
HSerPrint Hex( AMS_5600_getAddress )
HSerPrintCRLF
HSerPrint "Device Burn count     : "
HSerPrint AMS_5600_getBurnCount
HSerPrintCRLF
HSerPrint "Device Config         : "

regvalues = AMS_5600_getConf
HSerPrint ByteToBin(regvalues_h) 
HSerPrint " "
HSerPrint ByteToBin( regvalues ) 
HSerPrintCRLF

AMS_5600_setConf (  0xFF3F )  'Change PWM Frequency
regvalues = AMS_5600_getConf
HSerPrint "Device Config PWM     : "
HSerPrint ByteToBin(regvalues_h) 
HSerPrint " "
HSerPrint ByteToBin( regvalues ) 
HSerPrintCRLF


HSerPrint "Device Magnitude      : 0x"
HSerPrint Right(Hex( AMS_5600_getMagnitude_h) ,1)
HSerPrint Hex( AMS_5600_getMagnitude )
HSerPrintCRLF

HSerPrint "Device AGC            : " 
HSerPrint AMS_5600_getAgc
HSerPrintCRLF

HSerPrint "Magnet  Present       : " 
If AMS_5600_detectMagnet then 
    HSerPrint "Yes"
Else
    HSerPrint "No"
End If
HSerPrintCRLF

regvalues = AMS_5600_getMagnetStrength
HSerPrint "Magnet  Status        : " 
HSerPrint regvalues

Dir Porta.0 out
Dir Porta.1 out
Dir Porta.2 out
Dir Porta.3 out

Do

    valor = AMS_5600_getRawAngle

    If valor <> oldvalor then
        HSerPrintCRLF 
        HSerPrint valor
        If  [integer]valor - [integer]oldvalor > 0 Then
            HserPrint " +"
            Porta.0 = 1
            Porta.1 = 0
            
        Else
            HserPrint " -"
            Porta.0 = 0
            Porta.1 = 1

        End If
        oldvalor = valor
        
    End if
Loop

' ----- Support methods.  Subroutines and Functions
