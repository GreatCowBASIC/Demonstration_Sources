'''A demonstration program for GCBASIC.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program searches for I2C devices and display their adresses to the attached PC terminal.
'''This program uses the software implementation of I2C for the Microchip microprocessors.
'''The hardware serial connections are shown in the program.
'''@author  EvanV
'''@licence GPL
'''@version 1.0a
'''@date    14.12.15
'''********************************************************************************

; ----- Configuration
  #chip 16F17556
  #option explicit


    #startup InitPPS, 85
    #define PPSToolPart 16F17556

    Sub InitPPS
            
            // Module: EUSART
            RC6PPS = 0x0013    // TX1 > RC6

        End Sub
    // Add this code segment to your GCBASIC source program


; ----- Define Hardware settings
  ' Define I2C settings - CHANGE PORTS
 #define I2C_MODE Master
 #define I2C_DATA PORTC.5
 #define I2C_CLOCK PORTC.4
 #define I2C_DISABLE_INTERRUPTS ON

 ' THIS CONFIG OF THE SERIAL PORT WORKS WITH A  MAX232 (or equiv) THEN TO PC
 ' USART settings - CHANGE PORTS if required
 #define USART_BAUD_RATE 9600
 #define USART_TX_BLOCKING
 wait 500 ms

;----- Variables
  Dim DISPLAYNEWLINE as Byte


; ----- Main body of program commences here.
         ' Now assumes Serial Terminal is operational
  dim DeviceID as byte

'  HSerPrintCRLF
'  dim myMessage as String * 12
'  myMessage = "Software I2C"
'  HSerPrint myMessage
'  HSerPrintCRLF 2

    HSerPrintCRLF
    HSerPrint "Software I2C   Live Demo"
    HSerPrintCRLF 2

  HSerPrint "     "
  for DeviceID = 0 to 15
    HSerPrint hex(deviceID)
    HSerPrint " "
  next
  for DeviceID = 0 to 255
    DisplayNewLine = DeviceID % 16
    if DisplayNewLine = 0 Then
      HSerPrintCRLF
      HserPrint hex(DeviceID)
      HSerPrint ": "
    end if
    HSerPrint " "
    I2CStart
    I2CSend ( deviceID )

    if I2CSendState = True  then
      HSerPrint   hex(deviceID)
    else
      HSerPrint "--"
    end if
    I2CSend ( 0 )
    i2cstop


  Next
  HSerPrintCRLF 2
  HSerPrint   "End of Search"
  HSerPrintCRLF 2
  END

