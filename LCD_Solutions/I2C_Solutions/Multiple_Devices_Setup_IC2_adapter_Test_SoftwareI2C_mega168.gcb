'''A demonstration program for GCBASIC.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program demonstrates shows two LCD's (16x2 & 16x4) being driven using 2 LCD I2C adapters.  Up to four I2C adapters are supported.
'''This uses the GCB software implementation of TWI/IC2 for AVR devices.
'''Two types are supported as shown below:
'''- Set LCD_I0 to 10 for the YwRobot LCD1602 IIC V1 or the Sainsmart LCD_PIC I2C adapter.
'''- Set LCD_I0 to 12 for the Ywmjkdz I2C adapter with pot bent over top of chip.
'''- Use the following to address multiple I2C devices.
'''-     #define LCD_I2C_Address 0x4e            'This is the default value, change as appropiate
'''-     #define LCD_I2C_Address_1 0x4e          'set when multiples are on the bus
'''-     #define LCD_I2C_Address_2 0x4c
'''-     #define LCD_I2C_Address_3 0x4a
'''-     #define LCD_I2C_Address_4 0x49
'''
'''-     Use the command "LCD_I2C_Address_Current = LCD_I2C_Address[_1 | _2 | _3 | _4 ] to change the LCD output routines to a specific device.
'''-     Example:
'''-                     LCD_I2C_Address_Current = LCD_I2C_Address
'''-                     Print "LCD Address is now": LCDHex(  LCD_I2C_Address_Current, 2)
'''-                     LCD_I2C_Address_Current = LCD_I2C_Address_2
'''-                     Print "LCD Address is now": LCDHex(  LCD_I2C_Address_Current, 2)
'''@author 	EvanV
'''@licence	GPL
'''@version	1.0a
'''@date   	17.02.2015
'''********************************************************************************

; ----- Configuration

  #chip mega168, 16
  #option explicit


  ; ----- Define Hardware settings
  ' Define I2C settings - CHANGE PORTS
   #define I2C_MODE Master
   #define I2C_DATA PORTC.4
   #define I2C_CLOCK PORTC.5
   #define I2C_DISABLE_INTERRUPTS ON
   'Optionally, you can reduce the I2C timings.
   #define I2C_BIT_DELAY 0 us
   #define I2C_CLOCK_DELAY 1 us
   #define I2C_END_DELAY 0 us

  '''Set up LCD
   #define LCD_IO 10
   #define LCD_I2C_Address_1 0x4E
   #define LCD_I2C_Address_2 0x4C
   #define LCD_WIDTH 20                ;specified lcd width for clarity only.  20 is the default width

; ----- Constants
  'None required beyond the scope of the hardware.

;----- Variables
  Dim LCD_I2C_ADDRESS_CURRENT as Byte



; ----- Main body of program commences here.
do forever

    LCD_I2C_Address_Current = LCD_I2C_Address_1
    CLS
    PRINT "Great Cow BASIC"
    Locate 1,0
    PRINT "I2C LCD Driver"


    LCD_I2C_Address_Current = LCD_I2C_Address_2
    CLS
    PRINT "Great Cow BASIC"
    Locate 1,0
    Print "I2C LCD Driver"
    Locate 2,0
    Print  "By Anobium"
    Locate 3,0
    Print  "Feb 2019"
    Wait 3 s

    LCD_I2C_Address_Current = LCD_I2C_Address_1
    Repeat 2
      ClS
      Print "AVR"
      locate 1,0
      Print ChipNameStr + " @ "
      Print str(ChipMHz) + "Mhz"
      LCD_I2C_Address_Current = LCD_I2C_Address_2
    end repeat
    wait 3 s

    LCD_I2C_Address_Current = LCD_I2C_Address_1
    repeat 2
      CLS
      PRINT "Great Cow BASIC"
      Locate 1,0
      PRINT "Multiple Devices!"

      LCD_I2C_Address_Current = LCD_I2C_Address_2
    end repeat
    wait 3 s

    LCD_I2C_Address_Current = LCD_I2C_Address_1
    repeat 2
      cls
      Print "I2C address:"
      locate 1,0
      Print "0x": LCDHex(  LCD_I2C_Address_Current, 2)
      LCD_I2C_Address_Current = LCD_I2C_Address_2
    end repeat

    wait 3 s

 loop
