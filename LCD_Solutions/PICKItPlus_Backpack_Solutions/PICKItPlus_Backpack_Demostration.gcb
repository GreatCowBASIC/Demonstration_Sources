'''A demonstration program for GCBASIC.
'''--------------------------------------------------------------------------------
'''This program shows how to interface GCBASIC to the BackPack from PICKitPlus team
'''
'''
'''@author    EvanV   
'''@license   GPL
'''@version   1.00   
'''@date      2022-03-17
'''********************************************************************************


#CHIP 18F27Q43
#OPTION EXPLICIT
#CONFIG MCLRE=EXTMCLR


    'Generated by PIC PPS Tool for GCBASIC
    'PPS Tool version: 0.0.6.2
    'PinManager data: Not available (3)
    'Generated for 18f27q43
    '
    'Template comment at the start of the config file
    '
    #startup InitPPS, 85
    #define PPSToolPart 18f27q43

    Sub InitPPS
        
            'Module: UART pin directions
            Dir PORTC.6 Out    ' Make TX1 pin an output
            Dir PORTC.7 In    ' Make RX1 pin an input
            'Module: UART1
            RC6PPS = 0x0020    'TX1 > RC6
            U1RXPPS = 0x0017    'RC7 > RX1
        
    End Sub
    'Template comment at the end of the config file



    #DEFINE USART_BAUD_RATE 9600
    #DEFINE USART_TX_BLOCKING
    #DEFINE USART_DELAY OFF

    #DEFINE OUTPUT2 PORTB.3
    DIR OUTPUT2 OUT : SET OUTPUT2 OFF

    'Clear CLS
    printK107String("?f")
    wait 10 ms

    'Change Geometry
    printK107String("?G240")
    wait 100 ms

    'Changes the font size
    printK107String("?>2")
    wait 25 ms

    'Changes the cursor
    printK107String("?c0")
    wait 25 ms


    'test PWM
    printK107String("?B"+hex(128) )

    'test sound
    printK107String("?g" )


      'Some test code from here on down
      dim minstring as string
      dim mystring as string
      dim mysecs, mymins, mytime as word

      do Forever

            for mymins = 0 to 59
              for mysecs = 0 to 59

                mytime = ( mymins * 100 ) + mysecs

                select case mytime

                  case < 10:   mystring = "000" + str( mytime )
                  case < 100:  mystring = "00" + str( mytime )
                  case < 1000: mystring = "0" + str( mytime )
                  case else
                    mystring = str( mytime )
                end select

                ' printK107String("?y0?x12")
                ' printK107String( mystring )
                printK107String("?y0?x07")
                printK107String( left(mystring,2) )
                printK107String( ":")
                printK107String( right(mystring,2) )

                Output2 = !Output2

                'wait to simulate 1s wait.. an estimate. Use millis() to create an accurate clock
                wait 920 ms

              next
            next


      Loop

    Sub printK107String ( in K107inString as string )
        Dim ReadK107inStringCount as Byte
        ' Dim K107inString as String * 80

      for ReadK107inStringCount = 1 to LEN(K107inString)
          HSerSend K107inString( ReadK107inStringCount )
          wait 2 ms
      Next

    End Sub