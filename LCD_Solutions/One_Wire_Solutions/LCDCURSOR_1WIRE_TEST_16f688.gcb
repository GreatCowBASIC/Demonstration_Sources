'''A demonstration program for GCBASIC.
'''--------------------------------------------------------------------------------------------------------------------------------
'''This program is a GCBASIC implementation using a combined data-clock line; LCD_CD.
'''This mode, LCD_IO 1, is used when the LCD is connected through a shift register 74HC595,
'''as detailed in the GCB Help file.
'''This  method of driving an LCD requires an additional integrated circuit and other passive
'''components. This is not recommended for the beginner.
'''Evan R. Venn based on the works of Thomas Henry and Mike Predko
'''@author  EvanV plus works of ThomasH and MikeP
'''@licence GPL
'''@version 1.0
'''@date    09.08.2015
'''********************************************************************************

; ----- Configuration

 #chip 16F886, 16


 ; or

 ;#chip 16F1938, 32
 ;


  ;Setup LCD Parameters
  #define LCD_IO 1
  #define LCD_CD portb.0
  #define LCD_NO_RW

; ----- Main body of program commences here.

    Do Forever
        LCDBacklight On
        CLS
        PRINT "START TEST"
        locate 1,0
        PRINT "DISPLAY ON"
        wait 3 s

        CLS
        Locate 0,0
        Print "Cursor ON"
        Locate 1,0
        LCDcursor CursorOn
        wait 3 S

        CLS
        LCDcursor CursorOFF
        locate 0,0
        Print "Cursor OFF"
        wait 3 s

        CLS
        Locate 0,0
        Print "FLASH ON"
        Locate 1,0
        LCDcursor FLASHON
        wait 3 s

        CLS
        locate 0,0
        Print "FLASH OFF"
        LCDCURSOR FLASHOFF
        wait 3 sec

        Locate 0,0
        Print "CURSR & FLSH ON"
        locate 1,0
        LCDCURSOR CURSORON
        LCDCURSOR FLASHON
        Wait 3 sec

        Locate 0,0
        Print "CURSR & FLSH OFF"
        locate 1,0
        LCDCURSOR CursorOFF
        LCDCURSOR FLASHOFF
        Wait 3 sec

        CLS
        Locate 0,4
        PRINT "Flashing"
        Locate 1,4
        Print "Display"
        wait 500 ms

        repeat 10
            LCDCURSOR LCDOFF
            wait 500 ms
            LCDCURSOR LCDON
            wait 500 ms
        end repeat

        CLS
        Locate 0,0
        Print "DISPLAY OFF"
        Locate 1,0
        Print "FOR 5 SEC"
        Wait 2 SEC
        LCDCURSOR LCDOFF
        WAIT 5 s

        CLS
        Locate 0,0
        LCDCURSOR LCDON
        Print "Backlight off."
        LCDBacklight Off
        wait 3 s
        Locate 0,0
        Print "Backlight on. "
        LCDBacklight On
        WAIT 3 S
    loop
