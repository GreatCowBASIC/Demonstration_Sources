'''
'''
'''Enjoy.
'''
'''
'''************************************************************************

' ----- Configuration
#CHIP 16f886
#option Explicit 

/*
    Seeding for ELITE game.

    https://wiki.alioth.net/index.php/Random_number_generator

    https://www.bbcelite.com/deep_dives/galaxy_and_system_seeds.html

    The seeds for the selected system are stored at QQ15, while the seeds for the current galaxy are in QQ21.

    Here's a summary of which bits in which seeds are used to generate the various bits of data in the universe. 
    The routine names where these data are generated are shown on the right.

    s0_hi    s0_lo    s1_hi    s1_lo    s2_hi    s2_lo
    76543210 76543210 76543210 76543210 76543210 76543210

                                        ^^^----------- Species adjective 1 TT25
                                        ^^^-------------- Species adjective 2 TT25
        ^^----------------^^--------------------------- Species adjective 3 TT25
                                            ^^--------- Species type        TT25
                    ^^^^^^^^--------------^^^^--------- Average radius      TT25
                                ^^^--------------------- Government type     TT24
        ^^--------------------------------------------- Prosperity level    TT24
        ^----------------------------------------------- Economy type        TT24
                            ^^--------------------------- Tech level          TT24
                    ^^^^^^^^--------------------------- Galactic x-coord    TT24
    ^^^^^^^^--------------------------------------------- Galactic y-coord    TT24
                                                ^-^---- Long-range dot size TT22
                                                        ^ Short-range size    TT23
            ^------------------------------------------ Name length          cpl
                                        ^^^^^--------- Name token (x4)      cpl
        ^^^--------------------------------------------- Planet distance    SOLAR
                        ^^^--------------------------- Sun distance       SOLAR
                                            ^^--------- Sun x-y offset     SOLAR

    76543210 76543210 76543210 76543210 76543210 76543210
    s0_hi    s0_lo    s1_hi    s1_lo    s2_hi    s2_lo

    The seeds for Tibedied are s0 = &5A4A, s1 = &0248 and s2 = &B753, 
    and all 2000 of the unique systems in Elite are derived from this one set of seeds 
    (that's 256 systems in each of the eight galaxies)

    */

' ----- Main body of program commences here.

    // Define serial parameters
        #DEFINE USART_BAUD_RATE 9600
        #DEFINE USART_TX_BLOCKING
        #DEFINE USART_DELAY OFF

    Dim initialrun as Bit
    Dim s0, s1, s2, prevs0, prevs1, prevs2 as Word
    Dim twistcalc, ccount as Long
    Dim targetGalaxy as Byte
    // Alias the bytes to rotate each byte
    Dim s0L as Byte Alias s0
    Dim s0H as Byte Alias s0_H
    Dim s1L as Byte Alias s1
    Dim s1H as Byte Alias s1_H
    Dim s2L as Byte Alias s2
    Dim s2H as Byte Alias s2_H

    #define S0SEED  0x5A4A
    #define S1SEED  0x0248
    #define S2SEED  0xB753

    s0 = S0SEED
    s1 = S1SEED
    s2 = S2SEED
    initialrun = 1
    ccount = 0
    targetGalaxy = 0

    
    #ifdef USART_BAUD_RATE
        HSerPrint "Start in Tibedied Galaxy"
        HSerSend 13
        HSerSend 10
    #endif

Do

    if initialrun = 1 then
        initialrun = 0
    else 
        QQ21
    end if 
    QQ21  // The twist
    QQ21  // The twist
    QQ21  // The twist
    ccount++

    if ccount.17 = 1 Then  // everything repeats every 131072 times
        Ghy
        Show
    End if	

    // Are we back at the start ?
    if s0 = 0x5A4A then
        If s1 =  0x0248 then
            if s2 = 0xB753 then
                if targetGalaxy <> 0 Then
                    #ifdef USART_BAUD_RATE 
                        HSerPrint "Welcome back to Tibedied Galaxy, " 
                        HserPrint ( ccount / 4 ) - 1
                        HserPrint " systems in all galaxies "
                        HSerSend 13
                        HSerSend 10
                    #endif
                    end
                end if
            end if
        end if
    end if

Loop
End

Sub Show

    #ifdef USART_BAUD_RATE
        HserPrint hex(s0_h)
        HserPrint hex(s0)
        
        HSerSend 9
        HSerPrint hex(s1_h)
        HserPrint hex(s1)
        
        HSerSend 9
        HSerPrint hex(s2_h)
        HserPrint hex(s2)
        HSerSend 13
        HSerSend 10
        HSerSend 13
        HSerSend 10
    #endif

    End Sub

Sub QQ21  // The planetary twist

    Dim twistcalc as Long

    twistcalc = s0 + s1 + s2

    // twist
    s0 = s1
    s1 = s2
    twistcalc = [WORD]twistcalc
    s2 = twistcalc

    End Sub

Sub Ghy   // The galaxy twist

    /*
    The Ghy routine updates the galaxy seeds to point to the next galaxy. Using a galactic hyperdrive rotates each seed byte to the left, rolling each byte left within itself like this:

    01234567 -> 12345670
    to get the seeds for the next galaxy. So after 8 galactic jumps, the seeds roll round to those of the first galaxy again.

    The Rotate command will rotate variable one bit in a specified direction. 
    The bit shifted will be placed in the Carry bit of the Status register (STATUS.C).
    STATUS.C acts as a ninth bit of the variable that is being rotated.
    So, Rotate each byte and use STATUS.C to complete the Rotate.

    */

    targetGalaxy++
    #ifdef USART_BAUD_RATE
        HserPrint "Twist Galaxy to Galaxy "
        HserPrint targetGalaxy
        HSerSend 13
        HSerSend 10
    #endif

    s0 = S0SEED
    s1 = S1SEED
    s2 = S2SEED

    Set C Off
    repeat targetGalaxy
        rotate s0L left simple
    end Repeat

    Set C Off
    repeat targetGalaxy
        rotate s0H left simple
    end Repeat

    Set C Off
    repeat targetGalaxy
        rotate s1L left simple
    end Repeat

    Set C Off
    repeat targetGalaxy
        rotate s1H left simple
    end Repeat

    Set C Off
    repeat targetGalaxy
        rotate s2L left simple
    end Repeat

    Set C Off 
    repeat targetGalaxy
        rotate s2H left simple
    end Repeat

    End Sub
