'''A demonstration program for GCBASIC.
'''--------------------------------------------------------------------------------------------------------------------------------
'''Attach a servo motor to the port specified in the constant SERVOOUTCHANNEL1 - this program will make the servo sweep back and forth
'''
'''This is an example with operate at a chip frequency of 8 kHz, or above, as the time unit is 10us
'''This is an example uses an approximation of the period - there is compenstation to calculate the 20ms Period ( 50 hz ).
'''
'''This is also includes an adapted PulseOut to handle WORD time slices.
'''
'''@author      	EvanV plus works of HughC
'''@licence	GPL
'''@version	1.0a
'''@date   	02.01.2023
'''********************************************************************************

; ----- Configuration
 #chip 16F886, 8
 #option explicit

; ----- Constants
    // Change to suit requirements
    #DEFINE SERVOOUTCHANNEL1 PORTC.0
    DIR SERVOOUTCHANNEL1 OUT


    // Change to suit clock frequency and specifics of the servo - these parameters are for a Tower Pro SG90 servo
    // These values are in units of 10 as the base time unit is 10us 
    #DEFINE FULL_CW     70    // Adjust as needed for CW full position  - 50 is lower limit for TPSG9 servo
    #DEFINE FULL_CCW    230   // Adjust as Needed for CCW full position - 300 is upper limit for TPSG9 servo

; ----- Variables
    Dim PulseTime As WORD


; ----- Main body of program commences here.
  Do Forever

            'Rotate servo
            '(Gradually change pulse length from FULL_CW*10us to FULL_CCW*10us)
            For PulseTime = FULL_CW to FULL_CCW Step 5

                      'Send the right length pulse
                      PulseOut SERVOOUTCHANNEL1, PulseTime, 10us

                      'After sending pulse, wait a bit before sending the next one
                      Wait 20 ms

            Next
            wait 2 s
            'Then rotate it back
            '(Gradually change pulse length from FULL_CCW*10us to  FULL_CW*10us)
            For PulseTime = FULL_CCW to FULL_CW step -5

                      'Send the right length pulse
                      PulseOut SERVOOUTCHANNEL1, PulseTime, 10us

                      'After sending pulse, wait a bit before sending the next one
                      Wait 20 ms

            Next
            wait 2 s
  Loop

  end

macro PulseOut (Pin, localPulseTime as WORD, localTimeUnit)
  
  Set Pin On
  Repeat localPulseTime
    Wait 1 localTimeUnit
  End Repeat
  Set Pin Off

end macro

